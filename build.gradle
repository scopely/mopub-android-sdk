/**
 * Usage:
 * Set artifactoryDeployUser and artifactoryDeployPassword in ~/.gradle/gradle.properties
 *
 * ./gradlew uploadArchives
 *
 * Done!
 */

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.8.+'
    }
}

apply plugin: 'android-library'
apply plugin: 'maven'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.android.support:support-v4:13.0.+'
}

android {
    compileSdkVersion 17
    buildToolsVersion "19.0.1"
    sourceSets {
        main {
            manifest {
                srcFile 'mopub-sdk/AndroidManifest.xml'
            }
            java.srcDirs = ['mopub-sdk/src/main']
        }

        test {
            java.srcDirs = ['mopub-sdk/src/test']
        }
    }
}

artifactId = 'mopub-android-sdk'
version = '2.3.0.0'
group = 'com.mopub.mobileads'

android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    if (name.equals('debug')) {
        return;
    }
    def jarTask = project.tasks.create "jar${name.capitalize()}", Jar
    jarTask.dependsOn variant.javaCompile
    jarTask.from variant.javaCompile.destinationDir
    jarTask.appendix 'jar'
    artifacts.add('archives', jarTask)
}

uploadArchives {
    repositories {
        // the maven plugin features this deployer
        mavenDeployer {
            // we deploy to the release repository in this case
            repository(url: "https://scopelybin.artifactoryonline.com/scopelybin/libs-releases-local/") {
                authentication(userName: artifactoryDeployUser, password: artifactoryDeployPassword)
            }

            pom.project {
                artifactId 'mopub-android-sdk'
                name 'Mopub Android SDK'
                packaging 'jar'
            }
        }
    }
}

configurations {
    archives {
        extendsFrom configurations.default
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}
